# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20161217
pkgrel=1
pkgdesc="Arch Linux application database for AppStream-based software centers"
arch=(any)
url="http://www.archlinux.org"
license=(GPL)
depends=()
makedepends=()
source=(https://sources.archlinux.org/other/community/archlinux-appstream-data/$pkgver/{core,extra,community}.xml.gz
 https://sources.archlinux.org/other/community/archlinux-appstream-data/$pkgver/{core,extra,community}-icons-64x64.tar.gz
 https://sources.archlinux.org/other/community/archlinux-appstream-data/$pkgver/{core,extra,community}-icons-128x128.tar.gz)
noextract=({core,extra,community}.xml.gz {core,extra,community}-icons-{64x64,128x128}.tar.gz)
md5sums=('46985cb14943dae5009847988cd47b41'
         '9f6458c0c12bef08f62a592dbad87b1b'
         'd100d7c724849a50cdb72152a3a08a90'
         '31f6566d35ccd604be46ed5b1f813cdf'
         'bd530e456975f46a9fd888adeda25b86'
         '1f061af5aedad8057fa6f6782da826db'
         '31f6566d35ccd604be46ed5b1f813cdf'
         '1849e17932ebc8bae47c07705760d6e4'
         '055be93409afec419d86daa969f0bb00')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-64x64.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
  done
  cp *.xml.gz "$pkgdir"/usr/share/app-info/xmls/
}
