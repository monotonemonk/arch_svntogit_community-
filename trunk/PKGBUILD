# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Daniel Isenmann <daniel.isenmann [at] gmx.de>

pkgname=gcompris-data
pkgver=15.10
pkgrel=2
pkgdesc="Data files for GCompris"
arch=('any')
url="http://gcompris.net/"
license=('GPL3')
makedepends=('texinfo' 'texi2html' 'intltool' 'pygtk' 'librsvg'
             'sdl_mixer')
source=(http://gcompris.net/download/gtk/src/gcompris-$pkgver.tar.bz2)
sha256sums=('6aba73e2a1db0f50d43a40ee1a4a2b9d62a96e68542971b555c9debd8f50fc38')

build() {
  cd "$srcdir/gcompris-$pkgver"

  ./configure --prefix=/usr --enable-sdlmixer
  make LDFLAGS+=" -lgmodule-2.0"
}

package() {
  cd "$srcdir/gcompris-$pkgver"
  make DESTDIR="$pkgdir" install-data

  # Remove files shipped in the main gcompris package
  rm -r "$pkgdir"/usr/share/{appdata,applications,locale,pixmaps}
}

# vim:set ts=2 sw=2 et:
