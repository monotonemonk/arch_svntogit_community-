# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Vlad M. <vlad@archlinux.net>

pkgname=rustfmt
pkgver=0.7.1
pkgrel=1
pkgdesc="A tool for formatting Rust code according to style guidelines"
url="https://github.com/rust-lang-nursery/rustfmt"
makedepends=('cargo')
arch=('i686' 'x86_64')
license=('Apache' 'MIT')
source=("https://github.com/rust-lang-nursery/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('7b29f5eb92469cd97f9eea653421948f4b1fd4dd21668ca68d27f0035120ecab54d5b07237892b506b2948d0338caa6cf634a94cae746bcd222dc7333d53eaeb')

build() {
  cd "$pkgname-$pkgver"
  cargo build --release
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE-MIT LICENSE-APACHE
  cd target/release
  install -Dm755 -t "$pkgdir/usr/bin/" rustfmt cargo-fmt
}
