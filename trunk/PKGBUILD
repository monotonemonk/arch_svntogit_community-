# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Fernando Jim√©nez Solano <fjim@sdfeu.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=python2-pyacoustid
pkgver=1.1.4
pkgrel=1
pkgdesc='Bindings for Chromaprint acoustic fingerprinting and the Acoustid API'
arch=('any')
url='https://github.com/sampsyo/pyacoustid'
license=('MIT')
depends=('chromaprint')
makedepends=('python2-setuptools')
source=("pyacoustid-${pkgver}.tar.gz::https://github.com/sampsyo/pyacoustid/archive/v${pkgver}.tar.gz"
        'https://raw.github.com/sampsyo/beets/master/LICENSE')
sha256sums=('69e7e70d05a185e3602c7f752b2c9009c3caa17bbe7ddaed494d67fb63913b47'
            '69b9fa8fe9700714fc1f002aa6bfef097e5422372979195ab48956cd48e20aaf')

build() {
  cd pyacoustid-${pkgver}

  python2 setup.py build
}

package() {
  cd pyacoustid-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'

  install -dm 755 "${pkgdir}"/usr/share/licenses/python2-pyacoustid
  install -m 644 ../LICENSE "${pkgdir}"/usr/share/licenses/python2-pyacoustid/
}

# vim: ts=2 sw=2 et:
