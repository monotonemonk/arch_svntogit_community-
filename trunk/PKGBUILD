# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>

pkgname=sysvbanner
pkgver=1.0.15
pkgrel=2
pkgdesc='System-V banner clone'
arch=('i686' 'x86_64')
url='http://packages.qa.debian.org/s/sysvbanner.html'
depends=('glibc')
conflicts=('bsd-games')
license=('custom')
source=("http://ftp.debian.org/debian/pool/main/s/sysvbanner/${pkgname}_${pkgver}.tar.gz"
        '01-man.patch')
md5sums=('5a7bd216a42faa20bfd91268a77cc41d'
         '003aa4532e00c32f747dfba6566c094d')

prepare() {
  cd $pkgname-$pkgver
  # apply patch from the source array (should be a pacman feature)
  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      msg2 "Applying patch ${filename##*/}"
      patch -p1 -N -i "$srcdir/${filename##*/}"
    fi
  done
  :
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m444 "debian/copyright" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
