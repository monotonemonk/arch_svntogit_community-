# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Anatol Pomozov <anatol dot pomozov at gmail>

pkgname=meson
pkgver=0.35.1
pkgrel=1
pkgdesc='High productivity build system'
url='http://mesonbuild.com/'
arch=('any')
license=('Apache')
depends=('python' 'ninja')
makedepends=('python-setuptools')
checkdepends=('gcc-objc' 'vala' 'rust' 'gcc-fortran' 'mono' 'boost' 'qt5-base' 'git' 'gnustep-base' 'cython'
              'gtest' 'gmock' 'protobuf' 'wxgtk' 'python-gobject' 'gobject-introspection' 'pkgconfig')
source=(https://github.com/mesonbuild/meson/releases/download/${pkgver}/meson-${pkgver}.tar.gz{,.asc})
sha512sums=('474acc3f13024c8c9886a7666f290c24756cde7e47d553de9bf29740e53c699f1ca960a819ffb83af91fc83e201e2e0a1bb43c1a32d83e46d0edbd70b0cacb97'
            'SKIP')
validpgpkeys=('95181F4EED14FDF4E41B518D3BF4693BFEEB9428') # Jussi Pakkanen <jpakkane@gmail.com>

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|cython3|cython|g' -i 'test cases/python3/3 cython/meson.build'
}

check() {
  cd ${pkgname}-${pkgver}
  # for debugging set MESON_PRINT_TEST_OUTPUT
  ./run_tests.py
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1
  for f in "${pkgdir}"/usr/bin/*.py; do
    mv "${f}" "${f%%.py}"
  done
}

# vim: ts=2 sw=2 et:
