# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeffrey 'jf' Lim <jfs.world@gmail.com>

pkgname=wmii
pkgver=3.9.2
pkgrel=4
pkgdesc="A small, dynamic window manager for X11"
arch=('i686' 'x86_64')
license=('MIT')
url="http://wmii.suckless.org/"
depends=('libxft' 'libxrandr' 'libxinerama' 'dash')
options=('!makeflags')
validpgpkeys=('FF12470F30CF80D06FF72D04B1E4190FC02E8B8C')
source=(http://dl.suckless.org/${pkgname}/${pkgname}+ixp-${pkgver}.tbz{,.sig}
	'fix-freetype-include.patch')
sha256sums=('0466f1a5c534b0ee9fdace2c7a2d251845b7ec0ae80bdb8e68ae6584ebeebf2b'
            'SKIP'
            '30342db8b35d80f053510c9a466c58298d1b76ada5b8eeab988d179d106d065f')

build() {
  cd "$srcdir"/${pkgname}+ixp-${pkgver}

  patch -Np1 -i "$srcdir"/fix-freetype-include.patch

  sed -i 's|PREFIX = /usr/local|PREFIX = /usr|' config.mk
  sed -i 's|ETC = $(PREFIX)/etc|ETC = /etc|' config.mk
  sed -i 's# !=#!=#' mk/hdr.mk
  sed -i 's#env python#env python2#' alternative_wmiircs/python/wmiirc
  echo 'LIBS+=-lXrender -lX11' >>config.mk

  export MAKEFLAGS="-j1"
  make
}

package() {
  cd "$srcdir"/${pkgname}+ixp-${pkgver}
  make DESTDIR="$pkgdir" install
  install -Dm644 debian/file/wmii.desktop "$pkgdir"/usr/share/xsessions/${pkgname}.desktop
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
}
