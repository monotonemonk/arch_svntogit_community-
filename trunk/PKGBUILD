# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=python2-hidapi
pkgver=0.7.99.19
_pkgver=${pkgver%.*}.post${pkgver##*.}
pkgrel=1
pkgdesc="A Cython interface to the hidapi from signal11/hidapi"
arch=('i686' 'x86_64')
depends=('libusb' 'python2')
makedepends=('cython2' 'python2-setuptools')
url="https://github.com/trezor/cython-hidapi"
license=('custom')
source=(https://pypi.org/packages/source/h/${pkgname#python2-}/${pkgname#python2-}-$_pkgver.tar.gz)
sha256sums=('890dea8bd8c4d35e1f1b29a9a3431aa1ee79ee1baf86cacb5913983a5bec5689')

build() {
  cd "${pkgname#python2-}-$_pkgver"

  python2 setup.py build
}

package() {
  cd "${pkgname#python2-}-$_pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1

  install -dm 755 "$pkgdir/usr/share/licenses/$pkgname"
  echo "You are free to use cython-hidapi code for any purpose." > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
