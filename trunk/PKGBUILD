# $Id$
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=valgrind-multilib
pkgver=3.12.0
pkgrel=1
pkgdesc='A tool to help find memory-management problems in programs for multilib'
arch=(x86_64)
license=(GPL)
url='http://valgrind.org/'
depends=('lib32-glibc>=2.24' 'lib32-glibc<2.25' 'perl')
makedepends=(gdb openmpi gcc-multilib)
optdepends=('openmpi: MPI support')
# valgrind does not like stack protector flags
options=(!emptydirs !buildflags)
source=(http://valgrind.org/downloads/valgrind-$pkgver.tar.bz2)
sha1sums=('7a6878bf998c60d1e377a4f22ebece8d9305bda4')
 
provides=("valgrind=$pkgver-$pkgrel")
conflicts=('valgrind')

build() {
  cd valgrind-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --with-mpicc=mpicc
  make
}

package() {
  cd valgrind-$pkgver
  make DESTDIR="$pkgdir" install
}
