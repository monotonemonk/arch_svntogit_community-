# $Id: PKGBUILD 103944 2014-01-13 20:31:53Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=owncloud-app-contacts
epoch=2
pkgver=1.5.1
pkgrel=1
pkgdesc="Contacts app for owncloud"
arch=('any')
url="http://owncloud.org/"
license=('GPL')
depends=('owncloud')
makedepends=('bower' 'gulp' 'npm' 'git')
options=('!strip')
source=("$pkgname-$pkgver.tar.gz::https://github.com/owncloud/contacts/archive/v$pkgver.tar.gz")
sha256sums=('cd0baa37efdba15b967c8ba60e1476a1aeb29dd96c0cecf41cf6da7115b70b62')

build() {
  cd "$srcdir"/contacts-$pkgver
  bower install
  npm install
  gulp
}

package() {
  install -d "$pkgdir"/usr/share/webapps/owncloud/apps
  cp -a "$srcdir"/contacts-$pkgver "$pkgdir"/usr/share/webapps/owncloud/apps/contacts
}
