# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20161014
pkgrel=1
pkgdesc="Arch Linux application database for AppStream-based software centers"
arch=(any)
url="http://www.archlinux.org"
license=(GPL)
depends=()
makedepends=()
source=(http://sources.archlinux.org/other/community/archlinux-appstream-data/$pkgver/{core,extra,community}.xml.gz
 http://sources.archlinux.org/other/community/archlinux-appstream-data/$pkgver/{core,extra,community}-icons-64x64.tar.gz
 http://sources.archlinux.org/other/community/archlinux-appstream-data/$pkgver/{core,extra,community}-icons-128x128.tar.gz)
noextract=({core,extra,community}.xml.gz {core,extra,community}-icons-{64x64,128x128}.tar.gz)
md5sums=('ca704a3bb4d37355d752e51991d710d6'
         'eb137909b671142ee556e439e664a919'
         '626bdd9229204df6b9354fc58f4bc4ce'
         '31f6566d35ccd604be46ed5b1f813cdf'
         'a68367e6fde6b8d4db1ef7dcc2f4ae61'
         'bfc34f02e2685238fdf6b3828e638f19'
         '31f6566d35ccd604be46ed5b1f813cdf'
         '8be3a186f85b54ebf18c3d09a771ec14'
         '8be08f1694cebf74248b3b9296bf018c')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-64x64.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
  done
  cp *.xml.gz "$pkgdir"/usr/share/app-info/xmls/
}
