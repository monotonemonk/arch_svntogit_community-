# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Anatol Pomozov <anatol dot pomozov at gmail>

pkgname=meson
pkgver=0.38.0
pkgrel=1
pkgdesc='High productivity build system'
url='http://mesonbuild.com/'
arch=('any')
license=('Apache')
depends=('python' 'ninja')
makedepends=('python-setuptools')
checkdepends=('gcc-objc' 'vala' 'rust' 'gcc-fortran' 'mono' 'boost' 'qt5-base' 'git' 'gnustep-base'
              'cython' 'gtkmm3' 'gtest' 'gmock' 'protobuf' 'wxgtk' 'python-gobject' 'gobject-introspection'
              'itstool' 'gtk3')
source=(https://github.com/mesonbuild/meson/releases/download/${pkgver}/meson-${pkgver}.tar.gz{,.asc}
        ${pkgname}-${pkgver}-roundup.patch::https://github.com/mesonbuild/meson/commit/d2e4d597bb22b8a80d953f20546cee48cc7c79b4.patch)
sha512sums=('8d4b0bfbadc3ad7879c79bfe10e13ec8c6468922c08420b5855ffa3a12f2821ccd7bd17a3e962e33f7860cf160d4e6d0a91b231de0507b884dec74a7fbb87a7c'
            'SKIP'
            'aa212de4240a390db7306c68ece7e28c494240a9346536882837d9efa23ea9cb7f4b75b307e6561f93cec024a6c81982b0f060ebb24f842606ad46ebab221888')
validpgpkeys=('95181F4EED14FDF4E41B518D3BF4693BFEEB9428') # Jussi Pakkanen <jpakkane@gmail.com>

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|cython3|cython|g' -i 'test cases/python3/3 cython/meson.build'
  sed -r 's|^set |setlocal |g' -i syntax-highlighting/vim/*/meson.vim
  patch -p1 < "${srcdir}/${pkgname}-${pkgver}-roundup.patch"
}

check() {
  cd ${pkgname}-${pkgver}
  MESON_PRINT_TEST_OUTPUT=1 LC_CTYPE=en_US.UTF-8 ./run_tests.py
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1
  install -Dm 644 syntax-highlighting/vim/ftdetect/meson.vim -t "${pkgdir}/usr/share/vim/vimfiles/ftdetect"
  install -Dm 644 syntax-highlighting/vim/indent/meson.vim -t "${pkgdir}/usr/share/vim/vimfiles/indent"
  install -Dm 644 syntax-highlighting/vim/syntax/meson.vim -t "${pkgdir}/usr/share/vim/vimfiles/syntax"
}

# vim: ts=2 sw=2 et:
