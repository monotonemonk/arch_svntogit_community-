# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20160907
pkgrel=1
pkgdesc="Arch Linux application database for AppStream-based software centers"
arch=(any)
url="http://www.archlinux.org"
license=(GPL)
depends=()
makedepends=()
source=(http://sources.archlinux.org/other/community/archlinux-appstream-data/$pkgver/{core,extra,community}.xml.gz
 http://sources.archlinux.org/other/community/archlinux-appstream-data/$pkgver/{core,extra,community}-icons-64x64.tar.gz
 http://sources.archlinux.org/other/community/archlinux-appstream-data/$pkgver/{core,extra,community}-icons-128x128.tar.gz)
noextract=({core,extra,community}.xml.gz {core,extra,community}-icons-{64x64,128x128}.tar.gz)
md5sums=('9d2ec6705c9e1c9ba91135ef34c93dd9'
         '84e1e75a9b7ff43890c816519dc5bbc3'
         'fcc013c41b1b997833b8d72df6f53b67'
         '31f6566d35ccd604be46ed5b1f813cdf'
         '1c89d454074c951892cca9862da98f9f'
         'f7de400909ff64d069e2d32eb9597aa1'
         '31f6566d35ccd604be46ed5b1f813cdf'
         '414b75a3d54b9bd345b49002aaa1347e'
         '79f634504fbea150410d9ff90fc9910e')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-64x64.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
  done
  cp *.xml.gz "$pkgdir"/usr/share/app-info/xmls/
}
