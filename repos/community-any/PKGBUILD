# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Anatol Pomozov <anatol dot pomozov at gmail>

pkgname=meson
pkgver=0.35.0
pkgrel=1
pkgdesc='High productivity build system'
url='http://mesonbuild.com/'
arch=('any')
license=('Apache')
depends=('python' 'ninja')
makedepends=('python-setuptools')
checkdepends=('gcc-objc' 'vala' 'rust' 'gcc-fortran' 'mono' 'boost' 'qt5-base' 'git' 'gnustep-base' 'cython'
              'gtest' 'gmock' 'protobuf' 'wxgtk' 'python-gobject' 'gobject-introspection' 'pkgconfig')
source=(https://github.com/mesonbuild/meson/releases/download/${pkgver}/meson-${pkgver}.tar.gz{,.asc})
sha512sums=('513a8da6ee2c0c8a6c6bae198f328041b811293597308cb8c466af96faad8d2149cad098c659c7f2a6155111dbfc828686bbf4f053940ea12e65217e72e21a77'
            'SKIP')
validpgpkeys=('95181F4EED14FDF4E41B518D3BF4693BFEEB9428') # Jussi Pakkanen <jpakkane@gmail.com>

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|cython3|cython|g' -i 'test cases/python3/3 cython/meson.build'
}

check() {
  cd ${pkgname}-${pkgver}
  # for debugging set MESON_PRINT_TEST_OUTPUT
  ./run_tests.py
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1
  for f in "${pkgdir}"/usr/bin/*.py; do
    mv "${f}" "${f%%.py}"
  done
}

# vim: ts=2 sw=2 et:
