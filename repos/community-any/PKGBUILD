# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Anatol Pomozov <anatol dot pomozov at gmail>

pkgname=meson
pkgver=0.36.0
pkgrel=1
pkgdesc='High productivity build system'
url='http://mesonbuild.com/'
arch=('any')
license=('Apache')
depends=('python' 'ninja')
makedepends=('python-setuptools')
checkdepends=('gcc-objc' 'vala' 'rust' 'gcc-fortran' 'mono' 'boost' 'qt5-base' 'git' 'gnustep-base' 'cython'
              'gtest' 'gmock' 'protobuf' 'wxgtk' 'python-gobject' 'gobject-introspection' 'itstool')
source=(https://github.com/mesonbuild/meson/releases/download/${pkgver}/meson-${pkgver}.tar.gz{,.asc})
sha512sums=('efbd947b8ce5dc180e07807b955b084b7a1476caf36a36065a7dc5cd40007f65d4b99e3f2dbb63cb73082c172904e08f50d26f993d63f2531c370349b86835c9'
            'SKIP')
validpgpkeys=('95181F4EED14FDF4E41B518D3BF4693BFEEB9428') # Jussi Pakkanen <jpakkane@gmail.com>

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|cython3|cython|g' -i 'test cases/python3/3 cython/meson.build'
}

check() {
  cd ${pkgname}-${pkgver}
  # for debugging set MESON_PRINT_TEST_OUTPUT
  ./run_tests.py
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1
}

# vim: ts=2 sw=2 et:
