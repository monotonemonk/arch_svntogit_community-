# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>
# Contributor: Jonathan Lestrelin <zanko@daemontux.org>
# Contributor: Lucio Zara <pennega@gmail.com>

pkgname=spice-gtk3
pkgver=0.33
pkgrel=1
pkgdesc="Gtk client and libraries for SPICE remote desktop servers (gtk3 version)"
arch=('i686' 'x86_64')
url="http://spice-space.org"
license=('LGPL2.1')
depends=('celt0.5.1' 'dbus-glib' 'gtk3' 'libcacard' 'libpulse' 'usbredir' 'libsoup' 'spice-glib')
makedepends=('gobject-introspection' 'intltool' 'python2-pyparsing' 'spice-protocol' 'usbutils'
	     'vala' 'sdl' 'python2-six')
options=('!emptydirs')
source=("http://www.spice-space.org/download/gtk/spice-gtk-$pkgver.tar.bz2")
md5sums=('19e1b9aa6c59cab776ba5aa633491b7e')

build() {
  cd "$srcdir/spice-gtk-$pkgver"
  PYTHON=python2 ./configure --prefix=/usr \
    --disable-static --enable-vala --with-gtk=3.0 \
    --with-audio=pulse \
    --with-coroutine=gthread --sbindir=/usr/bin \
    --enable-smartcard
  make
}

package() {
  cd "$srcdir/spice-gtk-$pkgver"
  make DESTDIR="$pkgdir/" install
  pacman -Ql spice-glib | cut -f2 -d' ' | while read f; do
    [ -f $pkgdir/$f ] && rm -f $pkgdir/$f || true
    f=$(echo $f | sed 's|.gz$||')
    [ -f $pkgdir/$f ] && rm -f $pkgdir/$f || true
  done
}
