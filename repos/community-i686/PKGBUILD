# Contributor: Martin Wimpress <code@flexion.org>

_ver=1.16
_pkgbase=mate-user-share
pkgname=(${_pkgbase}-gtk3)
pkgver=${_ver}.0
pkgrel=2
pkgdesc="User level public file sharing via WebDAV for MATE (GTK3 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'mod_dnssd')
makedepends=('caja-gtk3' 'dbus-glib' 'intltool' 'itstool' 'libcanberra' 'libnotify')
optdepends=('caja-gtk3: File sharing extension')
groups=('mate-extra-gtk3')
conflicts=("${_pkgbase}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('7b241f0d969005653a164223b4d458beb9256910')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --libexec=/usr/lib/${_pkgbase} \
        --sysconfdir=/etc \
        --with-gtk=3.0 \
        --disable-bluetooth

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make  DESTDIR="${pkgdir}" install
}
