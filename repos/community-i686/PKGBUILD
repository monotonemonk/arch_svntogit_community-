# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Vlad M. <vlad@archlinux.net>

pkgname=rustfmt
pkgver=0.7
pkgrel=1
pkgdesc="A tool for formatting Rust code according to style guidelines"
url="https://github.com/rust-lang-nursery/rustfmt"
makedepends=('cargo')
arch=('i686' 'x86_64')
license=('Apache' 'MIT')
source=("https://github.com/rust-lang-nursery/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('ad6a62265804a3da316717542a2def25440064c15302f9a2a618a97e14d09b7e')

build() {
  cd "$pkgname-$pkgver"
  cargo build --release
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE-MIT LICENSE-APACHE
  cd target/release
  install -Dm755 -t "$pkgdir/usr/bin/" rustfmt cargo-fmt
}
