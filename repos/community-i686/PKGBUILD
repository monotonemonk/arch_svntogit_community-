# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qt5dxcb-plugin
pkgver=1.0
pkgrel=1
pkgdesc='Deepin Qt5 platform plugin'
arch=('i686' 'x86_64')
url="http://github.com/linuxdeepin/qt5dxcb-plugin"
license=('GPL3')
depends=('qt5-x11extras')
makedepends=('git' 'qt5-tools')
groups=('deepin')
source=("git+https://cr.deepin.io/qt5dxcb-plugin.git#tag=$pkgver"
        "git+https://github.com/zccrs/libqt5xcbqpa-dev.git")
sha256sums=('SKIP'
            'SKIP')

prepare() {
  cd qt5dxcb-plugin
  ln -s ../libqt5xcbqpa-dev ./
}

build() {
  cd qt5dxcb-plugin
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr
  make
}

package() {
  cd qt5dxcb-plugin
  make INSTALL_ROOT="$pkgdir" install
}
