# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-desktop
pkgver=4.0.0
pkgrel=4
pkgdesc='Deepin desktop-environment - Desktop module'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-desktop"
license=('GPL3')
depends=('gsettings-qt' 'deepin-daemon' 'deepin-tool-kit' 'startdde' 'deepin-dock' 'gtk2' 'qt5-svg'
         'qt5-x11extras' 'libqtxdg' 'deepin-file-manager' 'deepin-qt5integration')
makedepends=('git' 'boost' 'qt5-tools')
conflicts=('dde-desktop')
replaces=('dde-desktop')
groups=('deepin')
source=("git+https://cr.deepin.io/dde/dde-desktop.git#tag=$pkgver")
sha256sums=('SKIP')

build(){
  cd dde-desktop
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-desktop
  make INSTALL_ROOT="${pkgdir}" install
}
