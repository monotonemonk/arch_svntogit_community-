# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager-backend
pkgver=0.1.16
pkgrel=4
pkgdesc='deepin file manager backend'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-file-manager-backend"
license=('GPL3')
depends=('deepin-metacity' 'libcanberra' 'poppler-glib') # 'gcc-libs>=5.3.0-3')
makedepends=('git' 'deepin-dbus-factory' 'deepin-gir-generator' 'deepin-go-lib' 'deepin-api' 'go') # 'gcc-go')
source=("git+https://cr.deepin.io/deepin-file-manager-backend.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  go get github.com/howeyc/fsnotify github.com/mattn/go-sqlite3 gopkg.in/alecthomas/kingpin.v2
}

build() {
  cd deepin-file-manager-backend
  make # USE_GCCGO=1
}

package() {
  cd deepin-file-manager-backend
  make DESTDIR="$pkgdir" do-install
}
