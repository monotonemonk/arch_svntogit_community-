# Contributor: Martin Wimpress <code@flexion.org>
# Contributor: 3ED <krzysztof1987@gmail.com>

_pkgname=caja-actions
pkgname=${_pkgname}-gtk3
pkgver=1.8.1
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="An extension for Caja which allows arbitrary programs to be launched via context menu (GTK3 version)"
url="https://github.com/raveit65/caja-actions"
license=('GPL')
depends=('caja-gtk3' 'libgtop')
makedepends=('mate-common' 'python' 'yelp-tools')
replaces=("${_pkgbase}=1.8.1-2")
conflicts=("${_pkgbase}")
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/raveit65/${_pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('c9d09ef73979adbabaa7b4e618164b935cb6c7219e83803d7a61aa3fa498f5ad')

prepare() {
    cd "$srcdir/$_pkgname-$pkgver"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "$srcdir/$_pkgname-$pkgver"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgname} \
        --with-gtk=3

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "$srcdir/$_pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
