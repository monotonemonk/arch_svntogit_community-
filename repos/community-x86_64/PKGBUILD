# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Zygmunt Krynicki <me at zygoon dot pl>

pkgname=snap-confine
pkgver=1.0.41
pkgrel=1
pkgdesc="Confinement system for snap applications"
arch=('i686' 'x86_64')
url="git://github.com/snapcore/snap-confine"
license=('GPL3')
depends=('bash' 'libseccomp' 'libsystemd')
makedepends=('python-docutils' 'systemd')
checkdepends=('indent' 'shellcheck')
source=("https://github.com/snapcore/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('31bfcbe9affe11fb5fa84b6d4515827a')

build() {
  cd "$pkgname-$pkgver"

  # Confinement is disabled because arch doesn't yet have all the supporting
  # kernel patches.
  # Nvidia support is enabled to support proprietary nvidia drivers.
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/snapd \
    --disable-apparmor \
    --enable-nvidia-arch \
    --enable-merged-usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
