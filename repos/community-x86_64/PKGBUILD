# Contributor: Martin Wimpress <code@flexion.org>

_ver=1.14
_pkgbase=mate-panel
pkgname=${_pkgbase}-gtk3
pkgver=${_ver}.2
pkgrel=1
pkgdesc="The MATE Panel (GTK3 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'libwnck3' 'libcanberra' 'libmateweather-gtk3' 'libsm' 'mate-menus' 'mate-desktop-gtk3')
makedepends=('intltool' 'itstool' 'gobject-introspection')
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
groups=('mate-gtk3')
conflicts=("${_pkgbase}")
sha1sums=('c71956a9316d8db99f48822e0e1dde85dd523259')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --enable-introspection

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
