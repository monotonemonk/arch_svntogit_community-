# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Johan FÃ¶rberg <johan@forberg.se>

pkgname=zstd
pkgver=1.1.1
pkgrel=1
pkgdesc='Zstandard - Fast real-time compression algorithm'
arch=('i686' 'x86_64')
url='http://www.zstd.net/'
license=('BSD')
depends=('gcc-libs')
makedepends=('gtest')
source=("https://github.com/facebook/zstd/archive/v${pkgver}.tar.gz")
sha1sums=('dbd5287cde2c595e8e7b5ec509f1c6e8be8a0305')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    make
    make -C contrib/pzstd
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make PREFIX="/usr" DESTDIR="$pkgdir/" install
    install -D -m755 contrib/pzstd/pzstd "$pkgdir/usr/bin/pzstd"
    install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -D -m644 PATENTS "$pkgdir/usr/share/licenses/$pkgname/PATENTS"
}
